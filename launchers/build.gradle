plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

ext {
    lsp4jVersion = "0.12.0"
    ballerinaLangVersion = "2.0.0-beta.3"
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url 'https://maven.pkg.github.com/ballerina-platform/*'
        credentials {
            username System.getenv('packageUser')
            password System.getenv('packagePAT')
        }
    }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.12.0'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:0.12.0'
    implementation project(':server:server-core')
}

test {
    useJUnitPlatform()
}

task copyArtifacts(type: Copy) {
    dependsOn 'uberJar'
    from "$buildDir/libs/launchers-1.0-SNAPSHOT-uber.jar"
    into "../client/ls-client/"
}

build.finalizedBy(copyArtifacts)

task uberJar(type: Jar) {
    archiveClassifier = 'uber'

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}
